apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    signingConfigs {
        debug {
            storeFile file('/home/kevin/.android/debug.keystore')
        }
    }
    compileSdkVersion 28
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 3
        versionName "1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['myPublish']
    configurations = ['archives']
    pkg {
        repo = "Maven"
        name = "einvoice_sdk"
        vcsUrl = "https://github.com/kevinchung0921/einvoice_sdk.git"
        publish = true
    }
}

group = 'com.kevinchung0921'
version = '1.4'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        myPublish(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'eInvoice SDK'
                description = 'SDK for implement eInvoice app'
                url = 'https://github.com/kevinchung0921/einvoice_sdk'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kevinchung0921'
                        name = 'Kevin Chung'
                        email = 'kevinchung0921@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kevinchung0921/einvoice_sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/kevinchung0921/einvoice_sdk.git'
                    url = 'https://github.com/kevinchung0921/einvoice_sdk'
                }
            }
            artifact(sourceJar)
        }
    }
    repositories {
        maven {
            url = "https://bintray.com/kevinchung0921/Maven/"
        }
    }
}


